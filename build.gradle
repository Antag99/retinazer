

// Disable Java 8 doclint which throws errors
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

project(":retinazer") {
    apply plugin: "maven"
    apply plugin: "java"
    apply plugin: "eclipse"
    apply plugin: "idea"

    group = 'com.github.antag99'
    version = '0.1.0'

    compileJava {
        sourceCompatibility = 1.7
        targetCompatibility = 1.7
    }

    ext {
        jUnitVersion = "4.12"
        mockitoVersion = "1.10.19"
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourcesJar
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile "junit:junit:$jUnitVersion"
        testCompile "org.mockito:mockito-core:$mockitoVersion"
    }
}
